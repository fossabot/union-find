import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'java-library'

    id 'idea'
    id 'eclipse'

    id 'jacoco'

    id 'info.solidsoft.pitest' version '1.15.0'
    id 'me.champeau.jmh' version '0.7.2'

    // Publishing
    id 'com.vanniktech.maven.publish' version '0.28.0'

    id 'com.diffplug.spotless' version '6.25.0'
}

// Project properties
group = "$GROUP"
version = "$VERSION_NAME"
description = "$POM_DESCRIPTION"

// Source code properties
java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'
}

// Enable Spotless code formatting rules
spotless {
    java {
        googleJavaFormat()
    }

    lineEndings = 'UNIX'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

// Configure several tasks additionally for Gradle
tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = StandardCharsets.UTF_8.name()
    dependsOn(spotlessJavaCheck)
}

pitest {
    targetClasses = ['io.github.suppierk.*']
    threads = 1
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '1.2.1'
}

jmh {
    jvmArgsAppend = ['-Xmx2G']
    profilers = ['jfr']
    benchmarkMode = ['avgt']

    timeUnit = 'ns'
}